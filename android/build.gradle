buildscript {
  def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['RNGS_kotlinVersion']

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
  }
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

def isNewArchitectureEnabled() {
  // To opt-in for the New Architecture, you can either:
  // - Set `newArchEnabled` to true inside the `gradle.properties` file
  // - Invoke gradle with `-newArchEnabled=true`
  // - Set an environment variable `ORG_GRADLE_PROJECT_newArchEnabled=true`
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['RNGS_' + name]).toInteger()
}
def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  namespace "com.reactnativegooglesignin"

  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')

  // Used to override the NDK path/version on internal CI or by allowing
  // users to customize the NDK path/version from their root project (e.g. for M1 support)
  if (rootProject.hasProperty("ndkPath")) {
    ndkPath rootProject.ext.ndkPath
  }
  if (rootProject.hasProperty("ndkVersion")) {
    ndkVersion rootProject.ext.ndkVersion
  }

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault('minSdkVersion')
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    consumerProguardFiles 'src/main/consumer-rules.pro'
  }

  sourceSets.main {
    java {
      if (isNewArchitectureEnabled()) {
        srcDirs += 'src/newArch/java'
      } else {
        srcDirs += 'src/oldArch/java'
      }
    }
  }
}

repositories {
  google()
  mavenLocal()
  mavenCentral()
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  // https://developers.google.com/android/guides/releases
  implementation "com.google.android.gms:play-services-auth:${safeExtGet('googlePlayServicesAuthVersion', '21.4.0')}"
  // while we could parse the token on JS side, we would need to polyfill atob to do that.
  // it ends up being easier to do it on native side.
  implementation 'com.auth0.android:jwtdecode:2.0.2'
  // https://developers.google.com/identity/android-credential-manager/releases
  implementation "com.google.android.libraries.identity.googleid:googleid:1.1.1"
  // https://developer.android.com/jetpack/androidx/releases/credentials
  implementation "androidx.credentials:credentials:1.5.0"
  implementation "androidx.credentials:credentials-play-services-auth:1.5.0"

  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"
}
